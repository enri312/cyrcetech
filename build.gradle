plugins {
    id 'application'
    id 'eclipse'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.gradlex.extra-java-module-info' version '1.9'
}

repositories {
    mavenCentral()
}

dependencies {
    // PDFBox Maven dependency
    implementation 'org.apache.pdfbox:pdfbox:3.0.3'

    // JSON library for REST API communication
    implementation 'com.google.code.gson:gson:2.13.2'

    // JavaFX dependencies (gestionadas por el plugin)
    implementation 'org.openjfx:javafx-controls:25.0.1'
    implementation 'org.openjfx:javafx-fxml:25.0.1'

    // Testing dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform {
        excludeTags 'integration'
    }
}

// Separate task for integration tests (requires backend running)
tasks.register('integrationTest', Test) {
    description = 'Runs integration tests (requires backend at localhost:8080)'
    group = 'verification'
    
    useJUnitPlatform {
        includeTags 'integration'
    }
    
    shouldRunAfter test
}

javafx {
    version = "25.0.1"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

// Configuration for extra Java modules
extraJavaModuleInfo {

}

application {
    mainModule = 'com.cyrcetech'
    mainClass = 'com.cyrcetech.app.CyrcetechApp'
}